#!/usr/bin/env perl

use v5.32;
use warnings;
use experimental 'signatures';  # stable since v5.36

use POSIX qw(ttyname);
use Getopt::Long qw(GetOptions);
use File::Path qw(make_path);

use FindBin qw($RealBin);
use lib "$RealBin/lib";

use RevBank::Global;
use RevBank::Shell qw(abort);

our $VERSION = "10.0.2";

our %HELP1 = (
    "abort" => "Abort the current transaction",
);

GetOptions(
    "datadir=s"   => \$ENV{REVBANK_DATADIR},
    "plugindir=s" => \$ENV{REVBANK_PLUGINDIR},
    "c|command"   => \my $command,
);

if (not -e $ENV{REVBANK_DATADIR}) {
    make_path $ENV{REVBANK_DATADIR}
        or die "$0: $ENV{REVBANK_DATADIR}: Can't create directory.\n";
    spurt "accounts", "";
    spurt "nextid", "1";
    spurt $_, RevBank::FileIO::_slurp("$RealBin/data/$_")
        for qw(plugins products market);
}

-d $ENV{REVBANK_DATADIR}
    or die "$0: $ENV{REVBANK_DATADIR}: No such directory.\n";

-d $ENV{REVBANK_PLUGINDIR}
    or die "$0: $ENV{REVBANK_PLUGINDIR}: No such directory.\n";

$| = 1;
RevBank::Plugins->load;
call_hooks("startup");

if ($command) {
    my @words = RevBank::Prompt::split_input($ARGV[0]);
    @words and not ref $words[0] or die "$0: -c: Syntax error.\n";

    RevBank::Shell::exec(@words);
    exit;
}

if (not ttyname fileno STDIN) {
    warn "\e[31;1mNo controlling terminal, things will be borken!\n";
    warn "Use ssh -t (or RequestTTY in .ssh/config) for interactive sessions.\e[m\n";
}

RevBank::Shell::shell();
