#!/usr/bin/env perl

use v5.32;
use warnings;
use experimental 'signatures';  # stable since v5.36

use POSIX qw(ttyname);
use Getopt::Long qw(GetOptions :config posix_default no_ignore_case);

use FindBin qw($RealBin);
use lib "$RealBin/lib";
use RevBank::FileIO ();
use RevBank::Plugins ();
use RevBank::Shell qw(abort);

our $VERSION = "10.1.1";
$| = 1;

GetOptions(
    "datadir=s"   => \$ENV{REVBANK_DATADIR},
    "plugindir=s" => \$ENV{REVBANK_PLUGINDIR},
    "c|command=s" => \my $command,
) or die "Usage: $0 [--datadir <dir>] [--plugindir <dir>] [-c|--command <command>]\n";

RevBank::FileIO::create_datadir;
RevBank::Plugins::load;
RevBank::Plugins::call_hooks "startup";

if (defined $command) {
    my @words = RevBank::Prompt::split_input $command;
    @words and not ref $words[0] or die "$0: -c: Syntax error.\n";

    RevBank::Shell::exec @words;
    exit;
}

if (not ttyname fileno STDIN) {
    warn "\e[31;1mNo controlling terminal, things will be borken!\n";
    warn "Use ssh -t (or RequestTTY in .ssh/config) for interactive sessions.\e[m\n";
}

RevBank::Shell::shell;
